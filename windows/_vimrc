filetype off

"==============================
" plugin管理
"==============================
let s:dein_dir = expand('$VIM/plugins/suzuki/dein')
let s:dein_repo_dir = s:dein_dir . '/repos/github.com/Shougo/dein.vim'

if &runtimepath !~# '/dein.vim'
    if !isdirectory(s:dein_repo_dir)
      execute '!git clone https://github.com/Shougo/dein.vim' s:dein_repo_dir
    endif
    execute 'set runtimepath^=' . s:dein_repo_dir
endif

if dein#load_state(s:dein_dir)
    call dein#begin(s:dein_dir)

    let s:toml_path = '$VIM/dein_toml'
    let s:toml      = s:toml_path . '/dein.toml'
    let s:lazy_toml = s:toml_path . '/dein_lazy.toml'

    call dein#load_toml(s:toml,      {'lazy': 0})
    call dein#load_toml(s:lazy_toml, {'lazy': 1})

    call dein#end()
    call dein#save_state()
endif

if dein#check_install()
    call dein#install()
endif

"==============================
" 各種オプションの設定
"==============================
" タブ文字の表示幅
set tabstop=4
" タブ入力を複数の空白入力に置き換える
set expandtab
" Vimが挿入するインデントの幅
set shiftwidth=4
" 自動インデント
set autoindent
" スワップファイルは使わない
set noswapfile
" backupも不要
set nobackup
set noundofile
" 行番号表示
set number
" 文字列が右端に達しても折り返さない
set nowrap
" 上端/下端から指定行数分の視界を確保
set scrolloff=5
" ウインドウのタイトルバーにファイルのパス情報等を表示
set title
" コマンドラインに使われる画面上の行数
set cmdheight=2
" エディタウィンドウの末尾から2行目にステータスラインを常時表示させる
set laststatus=2
" ステータス行に表示させる情報
set statusline=%<%f\         " ファイルパス
set statusline+=%m           " 変更有無表示 ([+]<-こんなやつ)
set statusline+=%r           " 読み込み専用かどうかを表示
set statusline+=%h           " ヘルプページの場合はその旨表示
set statusline+=%w           " プレビューウィンドウの場合はその旨表示
set statusline+=%{'['.(&fenc!=''?&fenc:&enc).']['.&ff.']'} " エンコード
set statusline+=%=           " 以降がステータス行の右側の表示設定
set statusline+=[%l/%LL:%v]  " 行/列番号
if (dein#check_install('vim-fugitive') == 0)
    " 現在のgitブランチを表示
    set statusline+=%{fugitive#statusline()}
endif
" 入力中のコマンドを表示する
set showcmd
" インクリメンタルサーチ有効
set incsearch
" tab文字等を可視化
set list
set listchars=tab:>-,trail:-,extends:>,precedes:<,nbsp:%
" コマンド,検索パターンの履歴の最大数
set history=100
" 自動改行を抑止
set textwidth=0
" <>も対応括弧対象にする
set matchpairs& matchpairs+=<:>
" helpでハマる問題の対処
set notagbsearch
" file pathの区切り文字を\ではなく/にする(Windows向け)
set shellslash
" 各種文字コード
set encoding=utf-8
set fenc=utf-8
set fileencoding=utf-8
set fileformats=unix,dos,mac

" tag読み込み
au BufNewFile,BufRead *.c,*.h,*.cpp,*.hpp set tags=./tag_cxx.tags;
au BufNewFile,BufRead *.php set tags=./tag_php.tags;

" 保存時に末尾の空白を削除
autocmd BufWritePre * :%s/\s\+$//ge

" grep関連
set grepprg=grep\ -rnIH\ --exclude-dir=.svn\ --exclude-dir=.git
autocmd QuickfixCmdPost vimgrep copen
autocmd QuickfixCmdPost grep copen

" 全角spaceの色設定
if (dein#check_install('vim-zenspace') == 0)
    augroup vimrc-highlight
      autocmd!
      autocmd ColorScheme * highlight ZenSpace ctermbg=darkgray guibg=darkgray
    augroup END
endif

" Unite関連
if (dein#check_install('unite.vim') == 0)
    let g:unite_enable_start_insert=1
    let g:unite_source_history_yank_enable =1
    let g:unite_source_file_mru_limit = 200
    " file_mruの表示フォーマットを空にすると表示スピードが高速化される(らしい)
    let g:unite_source_file_mru_filename_format = ''
endif

" neocomplcache関連 (lua非対応のvimのみ対象)
if ((!has('lua')) && (dein#check_install('neocomplcache.vim') == 0))
    " 補完を有効化
    let g:neocomplcache_enable_at_startup = 1
    " AutoComplPopを封印
    let g:acp_enableAtStartup = 0
    " 大文字小文字を無視
    let g:neocomplcache_enable_smart_case = 1
    " "_"区切りの補完を有効化
    let g:neocomplcache_enable_underbar_completion = 1
    " シンタックスをキャッシュする際の最小文字数
    let g:neocomplcache_min_syntax_length = 3
    " 補完を自動的にlockするbuffer名のパターン
    let g:neocomplcache_lock_buffer_name_pattern = '\*ku\*'

    " ファイルタイプ毎のdictionary
    let g:neocomplcache_dictionary_filetype_lists = {
        \ 'default' : ''
        \ }

    let b:is_neocomplcache_configured = 1
endif

" neocomplete関連 (lua対応vimはこっち)
if ((has('lua')) && (dein#check_install('neocomplete.vim') == 0))
    " AutoComplPopを封印
    let g:acp_enableAtStartup = 0
    " 補完を有効化
    let g:neocomplete#enable_at_startup = 1
    " 大文字小文字を無視
    let g:neocomplete#enable_smart_case = 1
    " シンタックスをキャッシュする際の最小文字数
    let g:neocomplete#sources#syntax#min_keyword_length = 3

    " ファイルタイプ毎のdictionary
    let g:neocomplete#sources#dictionary#dictionaries = {
        \ 'default' : '',
        \ 'vimshell' : $VIM.'/.vimshell_hist',
        \ 'scheme' : $VIM.'/.gosh_completions'
            \ }

    " Define keyword.
    if !exists('g:neocomplete#keyword_patterns')
        let g:neocomplete#keyword_patterns = {}
    endif
    let g:neocomplete#keyword_patterns['default'] = '\h\w*'

    let b:is_neocomplete_configured = 1
endif

" neosnippet関連設定
if (exists('b:is_neocomplcache_configured') || exists('b:is_neocomplete_configured'))
    if (dein#check_install('neosnippet.vim') == 0)
        " 一部のfile typeに対して独自snippetを使う(他はsnippet-snippets任せ)
        let g:neosnippet#snippets_directory = '$VIM/snippets'
        let g:neosnippet#disable_runtime_snippets = {
            \ 'c'  : 1,
            \ 'cpp': 1,
        \}

        " Tab押下で次の入力位置にジャンプ
        smap <expr><TAB> neosnippet#expandable_or_jumpable() ?
        \ "\<Plug>(neosnippet_expand_or_jump)" : "\<TAB>"

        if has('conceal')
            set conceallevel=2 concealcursor=niv
        endif
    endif
endif

"==============================
" 各種キーバインド
"==============================
"--------------------------
" Normal Mode
"--------------------------
" .vimrcのreload
nnoremap <Space>s :source ~/.vimrc<CR>

" PPでyankレジスタ先頭の内容を貼り付け
nnoremap PP "0p

" yankレジスタの単語に置き換え
nnoremap <Space>p diwh"0p

" tag jump関連のキーバインドあれこれ
" [tag jump] カーソルの単語の定義先にジャンプ（複数候補はリスト表示）
nnoremap tj :exe("tjump ".expand('<cword>'))<CR>
" [tag back] tag stack を戻る
nnoremap tb :pop<CR>
" [tag next] tag stack を進む
nnoremap tn :tag<CR>
" [tag vertical] 縦にウィンドウを分割してジャンプ
nnoremap tv :vsp<CR> :exe("tjump ".expand('<cword>'))<CR>
" [tag horizon] 横にウィンドウを分割してジャンプ
nnoremap th :split<CR> :exe("tjump ".expand('<cword>'))<CR>
" [tag tab] 新しいタブでジャンプ
nnoremap tt :tab sp<CR> :exe("tjump ".expand('<cword>'))<CR>
" [tags list] tag list を表示
nnoremap tl :ts<CR>

" tab移動(左右)
nnoremap <silent> <Space>l gt
nnoremap <silent> <Space>h gT

" 文字列置換用
nnoremap <expr> <Space>r ':%s/' . expand('<cword>') . '/**/gc'

" Diff
nnoremap <expr> <Space>D ':vertical diffsplit '

" [ESC]2度押しで検索ハイライト解除
nnoremap <Esc><Esc> :noh<CR>

" 改行を含まない編集
" 1行削除
nnoremap <Space>d 0v$hx
" 1行yank
nnoremap <Space>y 0v$hy

" grep関連
nnoremap <expr> <Space>g ':vimgrep /\<'.expand('<cword>').'\>/j **/*.'.expand('%:e')
nnoremap <expr> <Space>G ':sil grep! '.expand('<cword>').' *'

" Unite関連
if (dein#check_install('unite.vim') == 0)
    " outline解析表示
    nnoremap <silent> <Space>o :<C-u>Unite -vertical -winwidth=50 outline<CR><ESC>
    " 他、諸々のUnite関連キーバインド
    nnoremap <silent> ,uy :<C-u>Unite history/yank<CR><ESC>
    nnoremap <silent> ,ub :<C-u>Unite buffer<CR><ESC>
    nnoremap <silent> ,uf :<C-u>UniteWithBufferDir -buffer-name=files file file/new<CR><ESC>
    nnoremap <silent> ,ur :<C-u>Unite -buffer-name=register register<CR><ESC>
    nnoremap <silent> ,um :<C-u>Unite file_mru buffer<CR><ESC>
    nnoremap <silent> ,up :<C-u>Unite file_rec/async:!<CR><ESC>
endif

"--------------------------
" Insert Mode
"--------------------------
" jjでインサートモード -> ノーマルモード
inoremap <silent> jj <ESC>

" 閉じ括弧の自動補完
inoremap {<Enter> {}<Left><CR><ESC><S-o>
inoremap (<Enter> ()<Left><CR><ESC><S-o>

" neocomplcache関連
if exists('b:is_neocomplcache_configured')
    " 補完を無かったことにする
    inoremap <expr><C-g> neocomplcache#undo_completion()
    " 補完候補から、共通する箇所を補完(シェル補完みたいなやつ)
    inoremap <expr><C-l> neocomplcache#complete_common_string()

    inoremap <silent> <CR> <C-r>=<SID>my_cr_function()<CR>
    function! s:my_cr_function()
      return neocomplcache#smart_close_popup() . "\<CR>"
    endfunction
    " TABキーで補完候補選択
    inoremap <expr><TAB>  pumvisible() ? "\<C-n>" : "\<TAB>"
    " Popupを閉じる
    inoremap <expr><C-h> neocomplcache#smart_close_popup()."\<C-h>"
    inoremap <expr><BS> neocomplcache#smart_close_popup()."\<C-h>"
    " 候補確定
    inoremap <expr><C-y>  neocomplcache#close_popup()
    " 候補をキャンセルし、Popupを閉じる
    inoremap <expr><C-e>  neocomplcache#cancel_popup()
endif

" neocomplete関連
if exists('b:is_neocomplete_configured')
    " 補完を無かったことにする
    inoremap <expr><C-g>     neocomplete#undo_completion()
    " 補完候補から、共通する箇所を補完(シェル補完みたいなやつ)
    inoremap <expr><C-l>     neocomplete#complete_common_string()

    inoremap <silent> <CR> <C-r>=<SID>my_cr_function()<CR>
    function! s:my_cr_function()
      return (pumvisible() ? "\<C-y>" : "" ) . "\<CR>"
    endfunction
    " TABキーで補完候補選択
    inoremap <expr><TAB>  pumvisible() ? "\<C-n>" : "\<TAB>"
    " Popupを閉じる
    inoremap <expr><C-h> neocomplete#smart_close_popup()."\<C-h>"
    inoremap <expr><BS> neocomplete#smart_close_popup()."\<C-h>"
    inoremap <expr><Space> pumvisible() ? "\<C-y>" : "\<Space>"
endif

" neosnippet関連設定
if (exists('b:is_neocomplcache_configured') || exists('b:is_neocomplete_configured'))
    if (dein#check_install('neosnippet.vim') == 0)
        " snippet候補を展開(マーカーのjump等にも使う)
        imap <C-k> <Plug>(neosnippet_expand_or_jump)
        smap <C-k> <Plug>(neosnippet_expand_or_jump)
        xmap <C-k> <Plug>(neosnippet_expand_target)
    endif
endif

"--------------------------
" Command Mode
"--------------------------
" Emacs風操作をパクる
cnoremap <C-a> <Home>
cnoremap <C-e> <End>

